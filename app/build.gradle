apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ru.scorpio92.vkmd2"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "2.3"
        multiDexEnabled true

        buildConfigField "String", "AUTHOR_URL", '"http://scorpio92.ru/about"'

        buildConfigField "String", "BASE_URL", '"https://m.vk.com"'
        buildConfigField "String", "LOGIN_URL", '"https://m.vk.com/login"'
        buildConfigField "String", "AUDIO_URL", '"https://m.vk.com/audio"'
        buildConfigField "String", "FEED_URL", '"https://m.vk.com/feed"'

        buildConfigField "int", "GET_AUDIO_OFFSET", '200'

        buildConfigField "String", "APP_FOLDER", '"VKMD"'
        buildConfigField "String", "MUSIC_FOLDER", '"music"'
        buildConfigField "String", "UPDATE_FOLDER", '"update"'
        buildConfigField "String", "DB_FOLDER", '"database"'
        buildConfigField "String", "DB_NAME", '"vkmd.db"'

        //FOR APP UPDATE
        buildConfigField "String", "GITHUB_REPO_RAW_URL", '"https://raw.githubusercontent.com/Scorpio92/vkmd2/master"'
        buildConfigField "String", "GITHUB_REPO_BUILD_CONFIG_URL", '"/app/build.gradle"'
        buildConfigField "String", "GITHUB_REPO_RELEASE_FOLDER", '"/release"'
        buildConfigField "String", "BUILD_MAIN_APP_NAME", '"vkmd-release"'
    }
    buildTypes {
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "vkmd-${variant.name}-${variant.versionName}.apk"
            }
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //ext.enableCrashlytics = false

            buildConfigField "boolean", "SAVE_FMC_TOKEN", 'false'
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "SAVE_FMC_TOKEN", 'true'
        }
    }

    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        stub {
            dimension "default"
            applicationIdSuffix = ".stub"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'stub') {
            variant.setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.firebase:firebase-messaging:17.3.2'
    implementation 'com.google.firebase:firebase-firestore:17.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.1.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.cleveroad:audiovisualization:1.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
}

apply plugin: 'com.google.gms.google-services'